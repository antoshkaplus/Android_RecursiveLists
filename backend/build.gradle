
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.3'
    }
}

repositories {
    maven {
        url 'https://maven-central.storage.googleapis.com'
    }
    mavenCentral()
    jcenter();
}

def projectId = 'antoshkaplus-recursivelists'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'
apply plugin: 'com.google.cloud.tools.appengine'

dependencies {

    implementation 'com.google.endpoints:endpoints-framework:2.0.10'
    //compile 'com.google.appengine:appengine-api-1.0-sdk:1.9.59'

    implementation 'javax.inject:javax.inject:1'
    implementation 'javax.servlet:javax.servlet-api:4.0.0'

    implementation 'com.googlecode.objectify:objectify:5.1.21'
    implementation 'com.google.apis:google-api-services-tasks:v1-rev46-1.22.0'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'com.googlecode.lambdaj:lambdaj:2.3.3'

    implementation project(path: ':bee')

    testImplementation 'com.google.appengine:appengine-api-labs:+'
    testImplementation 'com.google.appengine:appengine-api-stubs:+'
    testImplementation 'com.google.appengine:appengine-testing:+'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.googlecode.json-simple:json-simple:1.1.1'
}

appengine {
    deploy {   // deploy configuration
        version = 5
        stopPreviousVersion = true
        promote = true
    }
}

endpointsServer {
    hostname = "${projectId}.appspot.com"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// this replaces the ${endpoints.project.id} in appengine-web.xml and web.xml
task replaceProjectId(type: Copy) {
    from 'src/main/webapp/WEB-INF/'
    include '*.xml'
    into 'build/exploded-backend/WEB-INF'
    expand(endpoints:[project:[id:projectId]])
    filteringCharset = 'UTF-8'
}
assemble.dependsOn replaceProjectId